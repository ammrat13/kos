// PARAMETERS:
LOCAL holdDist TO 20.
LOCAL holdTol TO 1.
LOCAL speed TO 0.25.


// HELPER VARIABLES:
LOCAL topPID TO PIDLOOP(.5, .1, 3, -1, 1).
LOCAL starPID TO PIDLOOP(.5, .1, 3, -1, 1).


// HELPER FUNCTIONS:
LOCAL FUNCTION translate {
	DECLARE PARAMETER v.
	IF v:MAG > 1 {
		SET v TO v:NORMALIZED.
	}

	SET SHIP:CONTROL:FORE TO VDOT(SHIP:FACING:FOREVECTOR, v).
	SET SHIP:CONTROL:STARBOARD TO VDOT(SHIP:FACING:STARVECTOR, v).
	SET SHIP:CONTROL:TOP TO VDOT(SHIP:FACING:TOPVECTOR, v).
}

LOCAL FUNCTION translateTo {
	DECLARE PARAMETER x.
	translate(x:NORMALIZED*speed - (SHIP:OBT:VELOCITY:ORBIT - TARGET:SHIP:OBT:VELOCITY:ORBIT)).
}

// You must face your intended direction of travel
LOCAL FUNCTION translateLine {
	DECLARE PARAMETER x.

	topPID:UPDATE(TIME:SECONDS, VDOT(VXCL(SHIP:FACING:FOREVECTOR, x), SHIP:FACING:TOPVECTOR)).
	starPID:UPDATE(TIME:SECONDS, VDOT(VXCL(SHIP:FACING:FOREVECTOR, x), SHIP:FACING:STARVECTOR)).

	SET SHIP:CONTROL:FORE TO VDOT(SHIP:FACING:FOREVECTOR, x:NORMALIZED*speed - (SHIP:OBT:VELOCITY:ORBIT - TARGET:SHIP:OBT:VELOCITY:ORBIT)).
	SET SHIP:CONTROL:STARBOARD TO -1*starPID:OUTPUT.
	SET SHIP:CONTROL:TOP TO -1*topPID:OUTPUT.
}


// Credit to CheersKevin

LOCK STEERING TO LOOKDIRUP(-1*TARGET:FACING:FOREVECTOR, TARGET:FACING:TOPVECTOR).
LOCK relPos TO TARGET:POSITION - SHIP:POSITION.

CLEARSCREEN.

IF VANG(TARGET:FACING:FOREVECTOR, relPos) < 90 {
	PRINT "Performing sideswipe.".
	IF VANG(TARGET:FACING:STARVECTOR, relPos) < 90 {
		UNTIL (relPos - TARGET:FACING:STARVECTOR*holdDist):MAG <= holdTol{
			translateTo(relPos - TARGET:FACING:STARVECTOR*holdDist).
		}
	} ELSE {
		UNTIL (relPos + TARGET:FACING:STARVECTOR*holdDist) <= holdTol{
			translateTo(relPos + TARGET:FACING:STARVECTOR*holdDist).
		}
	}
}

PRINT "Translating in front of port.".
UNTIL (relPos + TARGET:FACING:FOREVECTOR*holdDist):MAG <= holdTol {
	translateTo(relPos + TARGET:FACING:FOREVECTOR*holdDist).
}

PRINT "Docking".
UNTIL NOT HASTARGET {
	translateLine(relPos + TARGET:FACING:FOREVECTOR*0.1).
}

UNLOCK STEERING .
UNLOCK THROTTLE .
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
SET SHIP:CONTROL:NEUTRALIZE TO TRUE.
